prototype(BetterEmbed.NeosEmbed:Component.Item) < prototype(Neos.Fusion:Component) {
    recordNode              = ${q(node).property('record')}

    identifier              = ${'embed-form-' + q(node).property('_identifier')}
    title                   = ${q(this.recordNode).property('title')}
    url                     = ${q(this.recordNode).property('url')}
    thumbnail               = ${q(this.recordNode).property('thumbnail')}
    thumbnailUrl            = ${q(this.recordNode).property('thumbnailUrl')}
    thumbnailContentType    = ${q(this.recordNode).property('thumbnailContentType')}
    thumbnailContent        = ${q(this.recordNode).property('thumbnailContent')}
    embedHtml               = ${q(this.recordNode).property('embedHtml')}
    body                    = ${q(this.recordNode).property('body')}
    datePublished           = ${q(this.recordNode).property('publishedAt')}
    authorName              = ${q(this.recordNode).property('authorName')}
    authorImage             = ${q(this.recordNode).property('authorImage')}
    authorUrl               = ${q(this.recordNode).property('authorUrl')}
    itemType                = ${q(this.recordNode).property('itemType')}

    placeholder = ${Translation.translate('placeholder.message', 'Please set a URL in the inspector panel', [], null, 'BetterEmbed.NeosEmbed')}

    @if.hasRecordOrInBackend = ${this.recordNode || node.context.inBackend}

    renderer = afx`
        <BetterEmbed.NeosEmbed:Component.Renderer @if.set={props.recordNode} @key='renderer' />
        <div @if.notSet={!props.recordNode} @key='placeholder' class='better-embed-placeholder'>
            <div class='better-embed-placeholder__heading'>{props.placeholder}</div>
        </div>
    `

    @cache {
        mode = 'cached'
        entryIdentifier {
            node = ${node}
        }
        entryTags {
            1 = ${Neos.Caching.nodeTag(node)}
            2 = ${Neos.Caching.nodeTypeTag('BetterEmbed.NeosEmbed:Record', node)}
        }
    }
}
